/-
Copyright (c) 2021 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import LeanCamCombi.SimplicialComplex.Basic

/-!
# Finite simplicial complexes
-/

open Set

variable {ð•œ E Î¹ : Type*}

namespace Geometry.SimplicialComplex
variable [OrderedRing ð•œ] [AddCommGroup E] [Module ð•œ E] {K Kâ‚ Kâ‚‚ : SimplicialComplex ð•œ E} {x t : E}
  {s t : Finset E} {A : Set (Finset E)} {m n : â„•}

/-- A simplicial complex is finite iff it has finitely many faces. -/
protected def Finite (K : SimplicialComplex ð•œ E) : Prop := K.faces.Finite

noncomputable def facesFinset (K : SimplicialComplex ð•œ E) (hS : K.Finite) : Finset (Finset E) :=
  hS.toFinset

@[simp]
lemma mem_facesFinset (hS : K.Finite) : s âˆˆ K.facesFinset hS â†” s âˆˆ K.faces :=
  Set.Finite.mem_toFinset _

/-- A simplicial complex `S` is locally finite at the face `s` iff `s` is a subface of finitely many
faces in `S`. -/
def LocallyFiniteAt (K : SimplicialComplex ð•œ E) (s : Finset E) : Prop :=
  {t âˆˆ K.faces | s âŠ† t}.Finite

/-- A simplicial complex `S` is locally finite at the face `s` iff `s` is a subface of infinitely
many faces in `S`. -/
def LocallyInfiniteAt (K : SimplicialComplex ð•œ E) (s : Finset E) : Prop :=
  {t âˆˆ K.faces | s âŠ† t}.Infinite

@[simp] lemma not_locallyInfiniteAt_iff : Â¬K.LocallyInfiniteAt s â†” K.LocallyFiniteAt s := not_not

/-- A simplicial complex is locally finite iff each of its nonempty faces belongs to finitely many
faces. -/
def LocallyFinite (K : SimplicialComplex ð•œ E) : Prop :=
  âˆ€ â¦ƒs : Finset _â¦„, s âˆˆ K.faces â†’ s.Nonempty â†’ K.LocallyFiniteAt s

lemma LocallyFiniteAt.mono (hX : K.LocallyFiniteAt s) (hXY : s âŠ† t) : K.LocallyFiniteAt t := by
  apply hX.subset
  rintro u âŸ¨_, _âŸ©
  exact âŸ¨â€¹u âˆˆ K.facesâ€º, hXY.trans â€¹t âŠ† uâ€ºâŸ©

lemma LocallyInfiniteAt.mono (hXY : s âŠ† t) (hY : K.LocallyInfiniteAt t) : K.LocallyInfiniteAt s :=
  fun t => hY <| LocallyFiniteAt.mono t hXY

protected lemma Finite.locallyFinite (hS : K.Finite) : K.LocallyFinite :=
  fun _s _hX _ => hS.subset fun _t hY => hY.1

/-- A simplicial complex is locally finite iff each point belongs to finitely many faces. -/
lemma locallyFinite_iff_mem_finitely_many_faces [DecidableEq E] :
    K.LocallyFinite â†” âˆ€ x, {s | s âˆˆ K.faces âˆ§ x âˆˆ convexHull ð•œ (s : Set E)}.Finite := by
  constructor
  Â· unfold LocallyFinite
    contrapose!
    rintro âŸ¨x, hxâŸ©
    by_cases hxspace : x âˆˆ K.space
    Â· obtain âŸ¨s, âŸ¨hX, hXhull, hXboundâŸ©, hXuniqueâŸ© := combiInteriors_partition hxspace
      simp at hXunique
      refine
        âŸ¨s, hX, Finset.nonempty_of_ne_empty ?_, fun hXlocallyfinite =>
          hx <| hXlocallyfinite.subset fun t hY => âŸ¨hY.1, ?_âŸ©âŸ©
      Â· rintro rfl
        simp at hXhull
      have hXYhull := K.inter_subset_convexHull hX hY.1 âŸ¨hXhull, hY.2âŸ©
      rw [â† Finset.coe_inter] at hXYhull
      by_contra hXY
      exact hXbound (mem_combiFrontier_iff.2 âŸ¨s âˆ© t, âŸ¨Finset.inter_subset_left,
        fun hXXY => hXY (Finset.subset_inter_iff.1 hXXY).2âŸ©, hXYhullâŸ©)
    Â· refine (hx ?_).elim
      convert finite_empty
      exact eq_empty_of_forall_not_mem fun s hX => hxspace <| mem_biUnion hX.1 hX.2
  Â· rintro hS s - âŸ¨x, hxâŸ©
    exact (hS x).subset fun t => And.imp_right fun ht => subset_convexHull _ _ <| ht hx

end Geometry.SimplicialComplex
