import Mathlib.Combinatorics.Colex
import LeanCamCombi.Mathlib.Data.Finset.Basic

variable {Î± Î² : Type*}

namespace Finset
namespace Colex
section PartialOrder
variable [PartialOrder Î±] [PartialOrder Î²] {f : Î± â†’ Î²} {ğ’œ ğ’œâ‚ ğ’œâ‚‚ : Finset (Finset Î±)}
  {s t u : Finset Î±} {a b : Î±}

lemma cons_le_cons (ha hb) : toColex (s.cons a ha) â‰¤ toColex (s.cons b hb) â†” a â‰¤ b := by
  obtain rfl | hab := eq_or_ne a b
  Â· simp
  classical
  rw [â†toColex_sdiff_le_toColex_sdiff', cons_sdiff_cons hab, cons_sdiff_cons hab.symm,
   singleton_le_singleton]

lemma cons_lt_cons (ha hb) : toColex (s.cons a ha) < toColex (s.cons b hb) â†” a < b :=
  lt_iff_lt_of_le_iff_le' (cons_le_cons _ _) (cons_le_cons _ _)

variable [DecidableEq Î±]

lemma insert_le_insert (ha : a âˆ‰ s) (hb : b âˆ‰ s) :
    toColex (insert a s) â‰¤ toColex (insert b s) â†” a â‰¤ b := by
  rw [â†cons_eq_insert _ _ ha, â†cons_eq_insert _ _ hb, cons_le_cons]

lemma insert_lt_insert (ha : a âˆ‰ s) (hb : b âˆ‰ s) :
    toColex (insert a s) < toColex (insert b s) â†” a < b := by
  rw [â†cons_eq_insert _ _ ha, â†cons_eq_insert _ _ hb, cons_lt_cons]

lemma erase_le_erase (ha : a âˆˆ s) (hb : b âˆˆ s) :
    toColex (s.erase a) â‰¤ toColex (s.erase b) â†” b â‰¤ a := by
  obtain rfl | hab := eq_or_ne a b
  Â· simp
  classical
  rw [â†toColex_sdiff_le_toColex_sdiff', erase_sdiff_erase hab hb, erase_sdiff_erase hab.symm ha,
   singleton_le_singleton]

lemma erase_lt_erase (ha : a âˆˆ s) (hb : b âˆˆ s) :
    toColex (s.erase a) < toColex (s.erase b) â†” b < a :=
  lt_iff_lt_of_le_iff_le' (erase_le_erase hb ha) (erase_le_erase ha hb)

end PartialOrder

section LinearOrder
variable [LinearOrder Î±] {s t : Finset Î±} {a : Î±}

instance [Fintype Î±] : BoundedOrder (Colex Î±) where
  top := toColex univ
  le_top _x := toColex_le_toColex_of_subset $ subset_univ _

@[simp] lemma toColex_univ [Fintype Î±] : toColex (univ : Finset Î±) = âŠ¤ := rfl
@[simp] lemma ofColex_top [Fintype Î±] : ofColex (âŠ¤ : Colex Î±) = univ := rfl

lemma lt_iff_exists_forall_lt_mem_iff_mem :
    toColex s < toColex t â†” âˆƒ w âˆˆ t, w âˆ‰ s âˆ§ âˆ€ â¦ƒaâ¦„, w < a â†’ (a âˆˆ s â†” a âˆˆ t) := by
  simp only [lt_iff_exists_forall_lt]
  refine âŸ¨fun h â†¦ ?_, ?_âŸ©
  Â· let u := (t \ s).filter fun w â†¦ âˆ€ a âˆˆ s, a âˆ‰ t â†’ a < w
    have mem_u {w : Î±} : w âˆˆ u â†” w âˆˆ t âˆ§ w âˆ‰ s âˆ§ âˆ€ a âˆˆ s, a âˆ‰ t â†’ a < w := by simp [and_assoc]
    have hu : u.Nonempty := h.imp fun _ â†¦ mem_u.2
    let m := max' _ hu
    have âŸ¨hmt, hms, hmâŸ© : m âˆˆ t âˆ§ m âˆ‰ s âˆ§ âˆ€ a âˆˆ s, a âˆ‰ t â†’ a < m := mem_u.1 $ max'_mem _ _
    refine âŸ¨m, hmt, hms, fun a hma â†¦ âŸ¨fun has â†¦ not_imp_comm.1 (hm _ has) hma.asymm, fun hat â†¦ ?_âŸ©âŸ©
    by_contra has
    have hau : a âˆˆ u := mem_u.2 âŸ¨hat, has, fun b hbs hbt â†¦ (hm _ hbs hbt).trans hmaâŸ©
    exact hma.not_le $ le_max' _ _ hau
  Â· rintro âŸ¨w, hwt, hws, hwâŸ©
    refine âŸ¨w, hwt, hws, fun a has hat â†¦ ?_âŸ©
    by_contra! hwa
    exact hat $ (hw $ hwa.lt_of_ne $ ne_of_mem_of_not_mem hwt hat).1 has

lemma erase_le_erase_min' (hst : toColex s â‰¤ toColex t) (hcard : s.card â‰¤ t.card) (ha : a âˆˆ s) :
    toColex (s.erase a) â‰¤
      toColex (t.erase $ min' t $ card_pos.1 $ (card_pos.2 âŸ¨a, haâŸ©).trans_le hcard) := by
  generalize_proofs ht
  set m := min' t ht
  -- Case on whether `s = t`
  obtain rfl | h' := eq_or_ne s t
  -- If `s = t`, then `s \ {a} â‰¤ s \ {m}` because `m â‰¤ a`
  Â· exact (erase_le_erase ha $ min'_mem _ _).2 $ min'_le _ _ $ ha
  -- If `s â‰  t`, call `w` the colex witness. Case on whether `w < a` or `a < w`
  replace hst := hst.lt_of_ne $ toColex_inj.not.2 h'
  simp only [lt_iff_exists_forall_lt_mem_iff_mem] at hst
  obtain âŸ¨w, hwt, hws, hwâŸ© := hst
  obtain hwa | haw := (ne_of_mem_of_not_mem ha hws).symm.lt_or_lt
  -- If `w < a`, then `a` is the colex witness for `s \ {a} < t \ {m}`
  Â· have hma : m < a := (min'_le _ _ hwt).trans_lt hwa
    refine (lt_iff_exists_forall_lt.2 âŸ¨a, mem_erase.2 âŸ¨hma.ne', (hw hwa).1 haâŸ©,
      not_mem_erase _ _, fun b hbs hbt â†¦ ?_âŸ©).le
    change b âˆ‰ t.erase m at hbt
    rw [mem_erase, not_and_or, not_ne_iff] at hbt
    obtain rfl | hbt := hbt
    Â· assumption
    Â· by_contra! hab
      exact hbt $ (hw $ hwa.trans_le hab).1 $ mem_of_mem_erase hbs
  -- If `a < w`, case on whether `m < w` or `m = w`
  obtain rfl | hmw : m = w âˆ¨ m < w := (min'_le _ _ hwt).eq_or_lt
  -- If `m = w`, then `s \ {a} = t \ {m}`
  Â· have : erase t m âŠ† erase s a
    Â· rintro b hb
      rw [mem_erase] at hb âŠ¢
      exact âŸ¨(haw.trans_le $ min'_le _ _ hb.2).ne', (hw $ hb.1.lt_of_le' $ min'_le _ _ hb.2).2 hb.2âŸ©
    rw [eq_of_subset_of_card_le this]
    rw [card_erase_of_mem ha, card_erase_of_mem (min'_mem _ _)]
    exact tsub_le_tsub_right hcard _
  -- If `m < w`, then `w` works as the colex witness for  `s \ {a} < t \ {m}`
  Â· refine (lt_iff_exists_forall_lt.2 âŸ¨w, mem_erase.2 âŸ¨hmw.ne', hwtâŸ©, mt mem_of_mem_erase hws,
      fun b hbs hbt â†¦ ?_âŸ©).le
    change b âˆ‰ t.erase m at hbt
    rw [mem_erase, not_and_or, not_ne_iff] at hbt
    obtain rfl | hbt := hbt
    Â· assumption
    Â· by_contra! hwb
      exact hbt $ (hw $ hwb.lt_of_ne $ ne_of_mem_of_not_mem hwt hbt).1 $ mem_of_mem_erase hbs

end LinearOrder
end Colex
end Finset
