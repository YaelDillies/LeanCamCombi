/-
Copyright (c) 2023 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Data.Set.Pointwise.SMul
import Mathlib.Order.Grade
import LeanCamCombi.Mathlib.Order.Chain
import LeanCamCombi.Mathlib.Order.RelIso.Group

/-!
# Additional constructions about flags

This file defines the action of order isomorphisms on flags and grades the elements of a flag.

## TODO

The file structure doesn't seem optimal. Maybe all the `Flag` material could move here, or to a
subfolder?
-/


open scoped Pointwise

variable {ğ•† Î± : Type*}

namespace Flag

/-!
### Action on flags

Order isomorphisms act on flags.
-/


section Preorder

variable [Preorder Î±]

instance : SMul (Î± â‰ƒo Î±) (Flag Î±) :=
  âŸ¨fun e => map eâŸ©

@[simp, norm_cast]
theorem coe_smul (e : Î± â‰ƒo Î±) (s : Flag Î±) : (â†‘(e â€¢ s) : Set Î±) = e â€¢ s :=
  rfl

instance : MulAction (Î± â‰ƒo Î±) (Flag Î±) := SetLike.coe_injective.mulAction _ coe_smul

end Preorder

/-!
### Grading a flag

A flag inherits the grading of its ambient order.
-/

section PartialOrder

variable [PartialOrder Î±] {s : Flag Î±}

@[simp]
theorem coe_covby_coe {a b : s} : (a : Î±) â‹– b â†” a â‹– b := by
  refine
    and_congr_right'
      âŸ¨fun h c hac => h hac, fun h c hac hcb =>
        @h âŸ¨c, mem_iff_forall_le_or_ge.2 fun d hd => ?_âŸ© hac hcbâŸ©
  classical
  obtain hda | had := le_or_lt (âŸ¨d, hdâŸ© : s) a
  Â· exact Or.inr ((Subtype.coe_le_coe.2 hda).trans hac.le)
  obtain hbd | hdb := le_or_lt b âŸ¨d, hdâŸ©
  Â· exact Or.inl (hcb.le.trans hbd)
  Â· cases h had hdb

@[simp]
theorem isMax_coe {a : s} : IsMax (a : Î±) â†” IsMax a :=
  âŸ¨fun h b hab => h hab, fun h b hab =>
    @h
      âŸ¨b,
        mem_iff_forall_le_or_ge.2 fun c hc => by
          classical exact Or.inr (hab.trans' <| h.isTop âŸ¨c, hcâŸ©)âŸ©
      habâŸ©

@[simp]
theorem isMin_coe {a : s} : IsMin (a : Î±) â†” IsMin a :=
  âŸ¨fun h b hba => h hba, fun h b hba =>
    @h
      âŸ¨b,
        mem_iff_forall_le_or_ge.2 fun c hc => by
          classical exact Or.inl (hba.trans <| h.isBot âŸ¨c, hcâŸ©)âŸ©
      hbaâŸ©

variable [Preorder ğ•†]

instance [GradeOrder ğ•† Î±] (s : Flag Î±) : GradeOrder ğ•† s :=
  .liftRight (â†‘) (Subtype.strictMono_coe _) fun _ _ => coe_covby_coe.2

instance [GradeMinOrder ğ•† Î±] (s : Flag Î±) : GradeMinOrder ğ•† s :=
  .liftRight (â†‘) (Subtype.strictMono_coe _) (fun _ _ => coe_covby_coe.2) fun _ => isMin_coe.2

instance [GradeMaxOrder ğ•† Î±] (s : Flag Î±) : GradeMaxOrder ğ•† s :=
  .liftRight (â†‘) (Subtype.strictMono_coe _) (fun _ _ => coe_covby_coe.2) fun _ => isMax_coe.2

instance [GradeBoundedOrder ğ•† Î±] (s : Flag Î±) : GradeBoundedOrder ğ•† s :=
  .liftRight (â†‘) (Subtype.strictMono_coe _) (fun _ _ => coe_covby_coe.2) (fun _ => isMin_coe.2)
    fun _ => isMax_coe.2

@[simp, norm_cast]
theorem grade_coe [GradeOrder ğ•† Î±] (a : s) : grade ğ•† (a : Î±) = grade ğ•† a := rfl

end PartialOrder

end Flag
