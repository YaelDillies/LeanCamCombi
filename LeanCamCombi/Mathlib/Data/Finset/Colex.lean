import Mathlib.Combinatorics.Colex
import Mathlib.Order.SupClosed
import LeanCamCombi.Mathlib.Data.Finset.Basic
import LeanCamCombi.Mathlib.Data.Finset.Slice

/-!
# Wolex

We define the wolex ordering for finite sets, and give a couple of important
lemmas and properties relating to it.

The wolex ordering likes to avoid large values - In the special case of `â„•`, it can be thought of as
the "binary" ordering. That is, order s based on `âˆ‘_{i âˆˆ s} 2^i`.
It's defined here in a slightly more general way, requiring only `LT Î±` in
the definition of wolex on `Finset Î±`. In the context of the Kruskal-Katona
lemma, we are interested in particular on how wolex behaves for sets of a
fixed size. If the size is 3, wolex on â„• starts
123, 124, 134, 234, 125, 135, 235, 145, 245, 345, ...

## Main statements

* Wolex order properties - linearity, decidability and so on.
* `Finset.Wolex.forall_lt_mono`: if `s < t` in wolex, and everything in `t` is `< a`, then
  everything in `s` is `< a`. This confirms the idea that an enumeration under wolex will exhaust
  all sets using elements `< a` before allowing `a` to be included.
* `FinsetoWolex t_image_lt_toWolex_image`: Strictly monotone functions preserve wolex.
* `Finset.sum_two_pow_le_iff_wolex_le`: wolex for Î± = â„• is the same as binary
  (this also proves binary expansions are unique)

## See also

Related files are:
* `data.list.lex`: Lexicographic order on lists.
* `data.pi.lex`: Lexicographic order on `Î â‚— i, Î± i`.
* `data.psigma.order`: Lexicographic order on `Î£' i, Î± i`.
* `data.sigma.order`: Lexicographic order on `Î£ i, Î± i`.
* `data.prod.lex`: Lexicographic order on `Î± Ã— Î²`.

## TODO

* Generalise `wolex.initSeg` so that it applies to `â„•`.

## References

* https://github.com/b-mehta/maths-notes/blob/master/iii/mich/combinatorics.pdf

## Tags

wolex, wolexicographic, binary
-/

variable {Î± Î² : Type*}

open Finset
open scoped BigOperators

namespace Finset

/-- Type synonym of `Finset Î±` equipped with the wolexicographic order rather than the inclusion
order. -/
def Wolex (Î±) := Finset Î±

instance : Inhabited (Finset.Wolex Î±) := inferInstanceAs (Inhabited (Finset Î±))

/-- `toWolex` is the "identity" function between `Finset Î±` and `Finset.Wolex Î±`. -/
def toWolex : Finset Î± â‰ƒ Wolex Î± := Equiv.refl _

/-- `ofWolex` is the "identity" function between `Finset.Wolex Î±` and `Finset Î±`. -/
def ofWolex : Wolex Î± â‰ƒ Finset Î± := Equiv.refl _

@[simp] lemma toWolex_symm_eq : (@toWolex Î±).symm = ofWolex := rfl
@[simp] lemma ofWolex_symm_eq : (@ofWolex Î±).symm = toWolex := rfl
@[simp] lemma toWolex_ofWolex (s : Wolex Î±) : toWolex (ofWolex s) = s := rfl
@[simp] lemma ofWolex_toWolex (s : Finset Î±) : ofWolex (toWolex s) = s := rfl
-- Tagged `nolint simpNF` because eligible for `dsimp`
@[simp, nolint simpNF] lemma toWolex_inj {s t : Finset Î±} : toWolex s = toWolex t â†” s = t := Iff.rfl
@[simp, nolint simpNF] lemma ofWolex_inj {s t : Wolex Î±} : ofWolex s = ofWolex t â†” s = t := Iff.rfl
lemma toWolex_ne_toWolex {s t : Finset Î±} : toWolex s â‰  toWolex t â†” s â‰  t := Iff.rfl
lemma ofWolex_ne_ofWolex {s t : Wolex Î±} : ofWolex s â‰  ofWolex t â†” s â‰  t := Iff.rfl

/-- Recursor for `Wolex Î±`. -/
@[elab_as_elim]
def Wolex.rec {C : Wolex Î± â†’ Sort*} (h : âˆ€ s, C (toWolex s)) : âˆ€ s, C s := h

namespace Wolex
section LT
variable [LT Î±] {s t : Finset Î±}

/-- `s` is less than `t` in the wolex ordering if the largest thing that's not in both sets is in t.
In other words, `max (s âˆ† t) âˆˆ t` (if the maximum exists). -/
instance instLT : LT (Wolex Î±) :=
  âŸ¨Î» s t â†¦ âˆƒ a, (âˆ€ â¦ƒxâ¦„, a < x â†’ (x âˆˆ ofWolex s â†” x âˆˆ ofWolex t)) âˆ§ a âˆ‰ ofWolex s âˆ§ a âˆˆ ofWolex tâŸ©

/-- We can define (â‰¤) in the obvious way. -/
instance instLE : LE (Wolex Î±) := âŸ¨Î» s t â†¦ s = t âˆ¨ s < tâŸ©

lemma lt_def {s t : Wolex Î±} :
    s < t â†” âˆƒ a, (âˆ€ â¦ƒxâ¦„, a < x â†’ (x âˆˆ ofWolex s â†” x âˆˆ ofWolex t)) âˆ§ a âˆ‰ ofWolex s âˆ§ a âˆˆ ofWolex t :=
  Iff.rfl

lemma le_def {s t : Wolex Î±} :
    s â‰¤ t â†” s = t âˆ¨
      âˆƒ a, (âˆ€ â¦ƒxâ¦„, a < x â†’ (x âˆˆ ofWolex s â†” x âˆˆ ofWolex t)) âˆ§ a âˆ‰ ofWolex s âˆ§ a âˆˆ ofWolex t :=
  Iff.rfl

lemma toWolex_lt_toWolex :
    toWolex s < toWolex t â†” âˆƒ k, (âˆ€ â¦ƒxâ¦„, k < x â†’ (x âˆˆ s â†” x âˆˆ t)) âˆ§ k âˆ‰ s âˆ§ k âˆˆ t := Iff.rfl

lemma toWolex_le_toWolex :
    toWolex s â‰¤ toWolex t â†” s = t âˆ¨ âˆƒ k, (âˆ€ â¦ƒxâ¦„, k < x â†’ (x âˆˆ s â†” x âˆˆ t)) âˆ§ k âˆ‰ s âˆ§ k âˆˆ t :=
  Iff.rfl

instance instIsIrrefl : IsIrrefl (Wolex Î±) (Â· < Â·) := âŸ¨by simp [lt_def]âŸ©

/-- Wolex doesn't care if you remove the other set -/
@[simp]
lemma sdiff_lt_sdiff_iff_lt [DecidableEq Î±] (s t : Finset Î±) :
    toWolex (s \ t) < toWolex (t \ s) â†” toWolex s < toWolex t := by
  rw [toWolex_lt_toWolex, toWolex_lt_toWolex]
  refine' exists_congr Î» k â†¦ _
  simp only [mem_sdiff, not_and, Classical.not_not]
  constructor
  Â· rintro âŸ¨z, kAB, kB, kAâŸ©
    refine' âŸ¨Î» x hx â†¦ _, kA, kBâŸ©
    specialize z hx
    tauto
  Â· rintro âŸ¨z, kA, kBâŸ©
    refine' âŸ¨Î» x hx => _, fun _ â†¦ kB, kB, kAâŸ©
    rw [z hx]

end LT

section LinearOrder

variable [LinearOrder Î±] [LinearOrder Î²] {f : Î± â†’ Î²} {ğ’œ ğ’œâ‚ ğ’œâ‚‚ : Finset (Finset Î±)} {s t : Finset Î±}
  {a b : Î±} {r : â„•}

instance : IsStrictTotalOrder (Wolex Î±) (Â· < Â·) where
  irrefl := irrefl_of (Â· < Â·)
  trans s t u := by
    rintro âŸ¨kâ‚, kâ‚z, notinA, inBâŸ© âŸ¨kâ‚‚, kâ‚‚z, notinB, inCâŸ©
    obtain h | h := (ne_of_mem_of_not_mem inB notinB).lt_or_lt
    Â· refine' âŸ¨kâ‚‚, Î» x hx â†¦ _, by rwa [kâ‚z h], inCâŸ©
      rw [â†kâ‚‚z hx]
      exact kâ‚z (h.trans hx)
    Â· refine' âŸ¨kâ‚, Î» x hx â†¦ _, notinA, by rwa [â†kâ‚‚z h]âŸ©
      rw [kâ‚z hx]
      exact kâ‚‚z (h.trans hx)
  trichotomous s t := by
    classical
    obtain rfl | hts := eq_or_ne t s
    Â· simp
    obtain âŸ¨k, hk, zâŸ© := exists_max_image (ofWolex t âˆ† ofWolex s) id (symmDiff_nonempty.2 hts)
    refine' (mem_symmDiff.1 hk).imp (Î» hk => âŸ¨k, Î» a ha â†¦ _, hk.2, hk.1âŸ©) fun hk â†¦
        Or.inr âŸ¨k, Î» a ha â†¦ _, hk.2, hk.1âŸ© <;>
      simpa [mem_symmDiff, not_or, iff_iff_implies_and_implies, and_comm, not_imp_not]
        using not_imp_not.2 (z a) ha.not_le

instance instDecidableLT : @DecidableRel (Wolex Î±) (Â· < Â·) := Î» s t â†¦
  decidable_of_iff'
    (âˆƒ k âˆˆ ofWolex t,
      (âˆ€ x âˆˆ ofWolex s âˆª ofWolex t, k < x â†’ (x âˆˆ ofWolex s â†” x âˆˆ ofWolex t)) âˆ§ k âˆ‰ ofWolex s) $
    exists_congr Î» a â†¦ by
      simp only [mem_union, exists_prop, or_imp, @and_comm (_ âˆˆ ofWolex t), and_assoc]
      exact and_congr_left' $ forall_congr' $ by tauto

instance instLinearOrder : LinearOrder (Wolex Î±) := linearOrderOfSTO (Â· < Â·)

instance instBot : Bot (Wolex Î±) where
  bot := toWolex âˆ…

@[simp] lemma toWolex_empty : toWolex (âˆ… : Finset Î±) = âŠ¥ := rfl
@[simp] lemma ofWolex_bot : ofWolex (âŠ¥ : Wolex Î±) = âˆ… := rfl

instance instOrderBot : OrderBot (Wolex Î±) where
  bot_le s := by
    induction' s using Finset.Wolex.rec with s
    rw [le_def]
    obtain rfl | hs := s.eq_empty_or_nonempty
    Â· simp
    refine' Or.inr âŸ¨max' _ hs, _, by simp, max'_mem _ _âŸ©
    simp only [max'_lt_iff, ofWolex_bot, not_mem_empty, ofWolex_toWolex, false_iff]
    exact Î» x hs hx â†¦ lt_irrefl _ $ hs _ hx

/-- Wolex doesn't care if you remove the other set -/
@[simp]
lemma sdiff_le_sdiff_iff_le (s t : Finset Î±) :
    toWolex (s \ t) â‰¤ toWolex (t \ s) â†” toWolex s â‰¤ toWolex t := by
  rw [le_iff_le_iff_lt_iff_lt, sdiff_lt_sdiff_iff_lt]

/-- If `s âŠ‚ t`, then `s` is less than `t` in the wolex order. Note the converse does not hold, as
`âŠ†` is not a linear order. -/
lemma wolex_lt_of_sSubset (h : s âŠ‚ t) : toWolex s < toWolex t := by
  rw [â†sdiff_lt_sdiff_iff_lt, sdiff_eq_empty_iff_subset.2 h.1, toWolex_empty, bot_lt_iff_ne_bot, â†
    toWolex_empty, toWolex_ne_toWolex]
  simpa using h.not_subset

/-- If `s âŠ† t`, then `s â‰¤ t` in the wolex order. Note the converse does not hold, as `âŠ†` is not a
linear order. -/
lemma wolex_le_of_subset (h : s âŠ† t) : toWolex s â‰¤ toWolex t := by
  rw [â†sdiff_le_sdiff_iff_le, sdiff_eq_empty_iff_subset.2 h, toWolex_empty]; exact bot_le

instance [Fintype Î±] : BoundedOrder (Wolex Î±) where
    top := toWolex univ
    le_top _x := wolex_le_of_subset (subset_univ _)

@[simp] lemma toWolex_univ [Fintype Î±] : toWolex (univ : Finset Î±) = âŠ¤ := rfl
@[simp] lemma ofWolex_top [Fintype Î±] : ofWolex (âŠ¤ : Wolex Î±) = univ := rfl

/-- `s < {a}` in wolex iff all elements of `s` are strictly less than `a`. -/
lemma toWolex_lt_singleton : toWolex s < toWolex {a} â†” âˆ€ x âˆˆ s, x < a := by
  simp only [toWolex_lt_toWolex, mem_singleton, â†and_assoc, exists_eq_right, â†not_le (a := a)]
  refine âŸ¨Î» t x hx hax â†¦ ?_, Î» h â†¦ âŸ¨Î» z hz â†¦ ?_, by simpa using h aâŸ©âŸ©
  Â· obtain hax | rfl := hax.lt_or_eq
    Â· exact hax.ne' $ (t.1 hax).1 hx
    Â· exact t.2 hx
  Â· exact âŸ¨fun i â†¦ ((h _ i) hz.le).elim, fun i â†¦ (hz.ne' i).elimâŸ©

/-- `{a} â‰¤ s` in wolex iff `r` contains an element greated than or equal to `a`. -/
lemma singleton_le_toWolex : (toWolex {a} : Wolex Î±) â‰¤ toWolex s â†” âˆƒ x âˆˆ s, a â‰¤ x := by
  simp only [â†not_lt, toWolex_lt_singleton, not_forall, exists_prop]

/-- Wolex is an extension of the base order. -/
lemma singleton_lt_singleton : (toWolex {a} : Wolex Î±) < toWolex {b} â†” a < b := by
  simp [toWolex_lt_singleton]

/-- Wolex is an extension of the base order. -/
lemma singleton_le_singleton : (toWolex {a} : Wolex Î±) â‰¤ toWolex {b} â†” a â‰¤ b := by
  rw [le_iff_le_iff_lt_iff_lt, singleton_lt_singleton]

/-- If `s` is before `t` in wolex, and everything in `t` is small, then everything in `s` is small.
-/
lemma forall_lt_mono (hâ‚ : toWolex s â‰¤ toWolex t) (hâ‚‚ : âˆ€ x âˆˆ t, x < a) : âˆ€ x âˆˆ s, x < a := by
  obtain rfl | âŸ¨k, z, -, hkâŸ© := toWolex_le_toWolex.1 hâ‚
  Â· assumption
  Â· refine' Î» x hx => lt_of_not_le fun h â†¦ h.not_lt $ hâ‚‚ x _
    rwa [â†z ((hâ‚‚ k hk).trans_le h)]

/-- Strictly monotone functions preserve the wolex ordering. -/
lemma toWolex_image_lt_toWolex_image (hf : StrictMono f) :
    toWolex (s.image f) < toWolex (t.image f) â†” toWolex s < toWolex t := by
  simp only [toWolex_lt_toWolex, not_exists, mem_image, exists_prop, not_and]
  constructor
  Â· rintro âŸ¨k, z, q, k', _, rflâŸ©
    exact âŸ¨k', Î» x hx => by simpa [hf.injective.eq_iff] using z (hf hx),
      fun t â†¦ q _ t rfl, â€¹k' âˆˆ tâ€ºâŸ©
  rintro âŸ¨k, z, ka, _âŸ©
  refine' âŸ¨f k, Î» x hx â†¦ _, _, k, â€¹k âˆˆ tâ€º, rflâŸ©
  Â· constructor
    all_goals
      rintro âŸ¨x', hx', rflâŸ©
      refine' âŸ¨x', _, rflâŸ©
      first
      | rwa [â†z _]
      | rwa [z _]
      rwa [StrictMono.lt_iff_lt hf] at hx
  Â· simp only [hf.injective, Function.Injective.eq_iff]
    exact Î» x hx â†¦ ne_of_mem_of_not_mem hx ka

/-- Strictly monotone functions preserve the wolex ordering. -/
lemma toWolex_image_le_toWolex_image (hf : StrictMono f) :
    toWolex (s.image f) â‰¤ toWolex (t.image f) â†” toWolex s â‰¤ toWolex t := by
  rw [le_iff_le_iff_lt_iff_lt, toWolex_image_lt_toWolex_image hf]

/-! ### Initial segments -/

/-- `ğ’œ` is an initial segment of the wolexigraphic order on sets of `r`, and that if `t` is below
`s` in wolex where `t` has size `r` and `s` is in `ğ’œ`, then `t` is also in `ğ’œ`. In effect, `ğ’œ` is
downwards closed with respect to wolex among sets of size `r`. -/
def IsInitSeg (ğ’œ : Finset (Finset Î±)) (r : â„•) : Prop :=
  (ğ’œ : Set (Finset Î±)).Sized r âˆ§
    âˆ€ â¦ƒs t : Finset Î±â¦„, s âˆˆ ğ’œ â†’ toWolex t < toWolex s âˆ§ t.card = r â†’ t âˆˆ ğ’œ

@[simp] lemma isInitSeg_empty : IsInitSeg (âˆ… : Finset (Finset Î±)) r := by simp [IsInitSeg]

/-- Initial segments are nested in some way. In particular, if they're the same size they're equal.
-/
lemma IsInitSeg.total (hâ‚ : IsInitSeg ğ’œâ‚ r) (hâ‚‚ : IsInitSeg ğ’œâ‚‚ r) : ğ’œâ‚ âŠ† ğ’œâ‚‚ âˆ¨ ğ’œâ‚‚ âŠ† ğ’œâ‚ := by
  classical
  simp_rw [â†sdiff_eq_empty_iff_subset, â†not_nonempty_iff_eq_empty]
  by_contra' h
  obtain âŸ¨âŸ¨s, hsâŸ©, t, htâŸ© := h
  rw [mem_sdiff] at hs ht
  obtain hst | rfl | hts := trichotomous_of (Â· < Â·) (toWolex s) (toWolex t)
  Â· exact hs.2 $ hâ‚‚.2 ht.1 âŸ¨hst, hâ‚.1 hs.1âŸ©
  Â· exact ht.2 hs.1
  Â· exact ht.2 $ hâ‚.2 hs.1 âŸ¨hts, hâ‚‚.1 ht.1âŸ©

variable [Fintype Î±]

/-- Gives all sets up to `s` with the same size as it: this is equivalent to
being an initial segment of wolex. -/
def initSeg (s : Finset Î±) : Finset (Finset Î±) :=
  univ.filter Î» t â†¦ s.card = t.card âˆ§ toWolex t â‰¤ toWolex s

@[simp]
lemma mem_initSeg : t âˆˆ initSeg s â†” s.card = t.card âˆ§ toWolex t â‰¤ toWolex s := by simp [initSeg]

lemma mem_initSeg_self : s âˆˆ initSeg s := by simp
@[simp] lemma initSeg_nonempty : (initSeg s).Nonempty := âŸ¨s, mem_initSeg_selfâŸ©

lemma isInitSeg_initSeg : IsInitSeg (initSeg s) s.card := by
  refine âŸ¨Î» t ht => (mem_initSeg.1 ht).1.symm, Î» tâ‚ tâ‚‚ htâ‚ htâ‚‚ â†¦ mem_initSeg.2 âŸ¨htâ‚‚.2.symm, ?_âŸ©âŸ©
  rw [mem_initSeg] at htâ‚
  exact htâ‚‚.1.le.trans htâ‚.2

lemma IsInitSeg.exists_initSeg (hğ’œ : IsInitSeg ğ’œ r) (hğ’œâ‚€ : ğ’œ.Nonempty) :
    âˆƒ s : Finset Î±, s.card = r âˆ§ ğ’œ = initSeg s := by
  have hs := sup'_mem (ofWolex â»Â¹' ğ’œ) (LinearOrder.supClosed _) ğ’œ hğ’œâ‚€ toWolex
    (Î» a ha â†¦ by simpa using ha)
  refine' âŸ¨_, hğ’œ.1 hs, _âŸ©
  ext t
  rw [mem_initSeg]
  refine' âŸ¨Î» p â†¦ _, _âŸ©
  Â· rw [hğ’œ.1 p, hğ’œ.1 hs]
    exact âŸ¨rfl, le_sup' _ pâŸ©
  rintro âŸ¨cards, leâŸ©
  obtain p | p := le.eq_or_lt
  Â· rwa [toWolex_inj.1 p]
  Â· exact hğ’œ.2 hs âŸ¨p, cards â–¸ hğ’œ.1 hsâŸ©

/-- Being a nonempty initial segment of wolex if equivalent to being an `initSeg`. -/
lemma isInitSeg_iff_exists_initSeg :
    IsInitSeg ğ’œ r âˆ§ ğ’œ.Nonempty â†” âˆƒ s : Finset Î±, s.card = r âˆ§ ğ’œ = initSeg s := by
  refine âŸ¨Î» hğ’œ â†¦ hğ’œ.1.exists_initSeg hğ’œ.2, ?_âŸ©
  rintro âŸ¨s, rfl, rflâŸ©
  exact âŸ¨isInitSeg_initSeg, initSeg_nonemptyâŸ©

end LinearOrder
end Wolex

open Wolex

/-!
### Wolex on `â„•`

The wolexicographic order agrees with the order induced by interpreting a set of naturals as a
binary expansion.
-/

section Nat

/-- For subsets of â„•, we can show that wolex is equivalent to binary. -/
lemma sum_two_pow_lt_iff_wolex_lt (s t : Finset â„•) :
    âˆ‘ i in s, 2 ^ i < âˆ‘ i in t, 2 ^ i â†” toWolex s < toWolex t := by
  have z : âˆ€ s t : Finset â„•, toWolex s < toWolex t â†’ âˆ‘ i in s, 2 ^ i < âˆ‘ i in t, 2 ^ i := by
    intro s t
    rw [â†sdiff_lt_sdiff_iff_lt, toWolex_lt_toWolex]
    rintro âŸ¨a, ha, has, hatâŸ©
    rw [â†sdiff_union_inter s t]
    conv_rhs => rw [â†sdiff_union_inter t s]
    rw [sum_union (disjoint_sdiff_inter _ _), sum_union (disjoint_sdiff_inter _ _), inter_comm,
      add_lt_add_iff_right]
    apply (@Nat.sum_two_pow_lt a (s \ t) _).trans_le
    Â· apply single_le_sum (Î» _ _ â†¦ Nat.zero_le _) hat
    intro x hx
    refine' (ne_of_mem_of_not_mem hx has).lt_or_lt.resolve_right $ Î» hax â†¦ _
    have := (ha hax).1 hx
    rw [mem_sdiff] at this hx
    exact hx.2 this.1
  refine' âŸ¨Î» h â†¦ (lt_trichotomy (toWolex s) $ toWolex t).resolve_right Î» hâ‚ â†¦
    hâ‚.elim _ (not_lt_of_gt h âˆ˜ z _ _), z s tâŸ©
  rw [toWolex_inj]
  rintro rfl
  exact irrefl _ h

/-- For subsets of â„•, we can show that wolex is equivalent to binary. -/
lemma sum_two_pow_le_iff_wolex_le (s t : Finset â„•) :
    âˆ‘ i in s, 2 ^ i â‰¤ âˆ‘ i in t, 2 ^ i â†” toWolex s â‰¤ toWolex t := by
  rw [le_iff_le_iff_lt_iff_lt, sum_two_pow_lt_iff_wolex_lt]

end Nat
end Finset
