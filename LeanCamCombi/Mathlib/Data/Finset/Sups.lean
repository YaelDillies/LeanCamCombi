import Mathlib.Data.Finset.Slice
import Mathlib.Data.Finset.Sups

/-!
# Set family operations

This file defines a few binary operations on `Finset Î±` for use in set family combinatorics.

## Main declarations

* `Finset.diffs`: Finset of elements of the form `a \ b` where `a âˆˆ s`, `b âˆˆ t`.
* `Finset.compls`: Finset of elements of the form `aá¶œ` where `a âˆˆ s`.

## Notation

We define the following notation in locale `FinsetFamily`:
* `s \\ t` for `Finset.diffs`
* `sá¶œË¢` for `Finset.compls`

## References

[B. BollobÃ¡s, *Combinatorics*][bollobas1986]
-/

-- TODO: Is there a better spot for those two instances?
namespace Finset
variable {Î± : Type*} [Preorder Î±] [@DecidableRel Î± (Â· â‰¤ Â·)] {s : Finset Î±}

instance decidablePredMemUpperClosure : DecidablePred (Â· âˆˆ upperClosure (s : Set Î±)) :=
  fun _ â†¦ decidableExistsAndFinset
#align finset.decidable_pred_mem_upper_closure Finset.decidablePredMemUpperClosure

instance decidablePredMemLowerClosure : DecidablePred (Â· âˆˆ lowerClosure (s : Set Î±)) :=
  fun _ â†¦ decidableExistsAndFinset
#align finset.decidable_pred_mem_lower_closure Finset.decidablePredMemLowerClosure

end Finset

open Fintype Function
open scoped FinsetFamily

variable {F Î± Î² : Type*} [DecidableEq Î±] [DecidableEq Î²]

namespace Finset
section SemilatticeSup
variable [SemilatticeSup Î±] [SemilatticeSup Î²] [SupHomClass F Î± Î²] {s : Finset Î±}

lemma image_sups (f : F) (s t : Finset Î±) : image f (s âŠ» t) = image f s âŠ» image f t :=
  image_imageâ‚‚_distrib $ map_sup f

lemma map_sups (f : F) (hf) (s t : Finset Î±) :
    map âŸ¨f, hfâŸ© (s âŠ» t) = map âŸ¨f, hfâŸ© s âŠ» map âŸ¨f, hfâŸ© t := by
  simpa [map_eq_image] using image_sups f s t

lemma subset_sups_self : s âŠ† s âŠ» s := Î» _a ha â†¦ mem_sups.2 âŸ¨_, ha, _, ha, sup_idemâŸ©

lemma filter_sups_le [@DecidableRel Î± (Â· â‰¤ Â·)] (s t : Finset Î±) (a : Î±) :
    (s âŠ» t).filter (Â· â‰¤ a) = s.filter (Â· â‰¤ a) âŠ» t.filter (Â· â‰¤ a) := by
  ext b
  simp only [mem_filter, mem_sups]
  constructor
  Â· rintro âŸ¨âŸ¨b, hb, c, hc, rflâŸ©, haâŸ©
    rw [sup_le_iff] at ha
    exact âŸ¨_, âŸ¨hb, ha.1âŸ©, _, âŸ¨hc, ha.2âŸ©, rflâŸ©
  Â· rintro âŸ¨b, hb, c, hc, _, rflâŸ©
    exact âŸ¨âŸ¨_, hb.1, _, hc.1, rflâŸ©, _root_.sup_le hb.2 hc.2âŸ©

variable [Fintype Î±]

@[simp] lemma univ_sups_univ : (univ : Finset Î±) âŠ» univ = univ := top_le_iff.1 subset_sups_self

end SemilatticeSup

section SemilatticeInf
variable [SemilatticeInf Î±] [SemilatticeInf Î²] [InfHomClass F Î± Î²] {s : Finset Î±}

lemma image_infs (f : F) (s t : Finset Î±) : image f (s âŠ¼ t) = image f s âŠ¼ image f t :=
  image_imageâ‚‚_distrib $ map_inf f

lemma map_infs (f : F) (hf) (s t : Finset Î±) :
    map âŸ¨f, hfâŸ© (s âŠ¼ t) = map âŸ¨f, hfâŸ© s âŠ¼ map âŸ¨f, hfâŸ© t := by
  simpa [map_eq_image] using image_infs f s t

lemma subset_infs_self : s âŠ† s âŠ¼ s := Î» _a ha â†¦ mem_infs.2 âŸ¨_, ha, _, ha, inf_idemâŸ©

lemma filter_infs_ge [@DecidableRel Î± (Â· â‰¤ Â·)] (s t : Finset Î±) (a : Î±) :
    (s âŠ¼ t).filter (a â‰¤ Â·) = s.filter (a â‰¤ Â·) âŠ¼ t.filter (a â‰¤ Â·) := by
  ext b
  simp only [mem_filter, mem_infs]
  constructor
  Â· rintro âŸ¨âŸ¨b, hb, c, hc, rflâŸ©, haâŸ©
    rw [le_inf_iff] at ha
    exact âŸ¨_, âŸ¨hb, ha.1âŸ©, _, âŸ¨hc, ha.2âŸ©, rflâŸ©
  Â· rintro âŸ¨b, hb, c, hc, _, rflâŸ©
    exact âŸ¨âŸ¨_, hb.1, _, hc.1, rflâŸ©, _root_.le_inf hb.2 hc.2âŸ©

variable [Fintype Î±]

@[simp] lemma univ_infs_univ : (univ : Finset Î±) âŠ¼ univ = univ := top_le_iff.1 subset_infs_self

end SemilatticeInf

variable [DecidableEq Î±] {ğ’œ â„¬ : Finset (Finset Î±)} {s t : Finset Î±} {a : Î±}

@[simp] lemma powerset_union (s t : Finset Î±) : (s âˆª t).powerset = s.powerset âŠ» t.powerset := by
  ext u
  simp only [mem_sups, mem_powerset, le_eq_subset, sup_eq_union]
  refine' âŸ¨Î» h â†¦ âŸ¨_, inter_subset_left _ u, _, inter_subset_left _ u, _âŸ©, _âŸ©
  Â· rwa [â†inter_distrib_right, inter_eq_right_iff_subset]
  Â· rintro âŸ¨v, hv, w, hw, rflâŸ©
    exact union_subset_union hv hw

@[simp] lemma powerset_inter (s t : Finset Î±) : (s âˆ© t).powerset = s.powerset âŠ¼ t.powerset := by
  ext u
  simp only [mem_infs, mem_powerset, le_eq_subset, inf_eq_inter]
  refine' âŸ¨Î» h â†¦ âŸ¨_, inter_subset_left _ u, _, inter_subset_left _ u, _âŸ©, _âŸ©
  Â· rwa [â†inter_inter_distrib_right, inter_eq_right_iff_subset]
  Â· rintro âŸ¨v, hv, w, hw, rflâŸ©
    exact inter_subset_inter hv hw

@[simp] lemma powerset_sups_powerset_self (s : Finset Î±) :
    s.powerset âŠ» s.powerset = s.powerset := by simp [â†powerset_union]

@[simp] lemma powerset_infs_powerset_self (s : Finset Î±) :
    s.powerset âŠ¼ s.powerset = s.powerset := by simp [â†powerset_inter]

lemma union_mem_sups : s âˆˆ ğ’œ â†’ t âˆˆ â„¬ â†’ s âˆª t âˆˆ ğ’œ âŠ» â„¬ := sup_mem_sups
lemma inter_mem_infs : s âˆˆ ğ’œ â†’ t âˆˆ â„¬ â†’ s âˆ© t âˆˆ ğ’œ âŠ¼ â„¬ := inf_mem_infs

section SemilatticeInf
variable [SemilatticeInf Î±]

end SemilatticeInf
end Finset

namespace Finset
section Diffs
variable [GeneralizedBooleanAlgebra Î±] (s sâ‚ sâ‚‚ t tâ‚ tâ‚‚ u v : Finset Î±)

/-- `s \\ t` is the finset of elements of the form `a \ b` where `a âˆˆ s`, `b âˆˆ t`. -/
def diffs : Finset Î± â†’ Finset Î± â†’ Finset Î± := imageâ‚‚ (Â· \ Â·)

@[inherit_doc]
scoped[FinsetFamily] infixl:74 " \\\\ " => Finset.diffs
  -- This notation is meant to have higher precedence than `\` and `âŠ“`, but still within the
  -- realm of other binary notation

open FinsetFamily

variable {s t} {a b c : Î±}

@[simp] lemma mem_diffs : c âˆˆ s \\ t â†” âˆƒ a âˆˆ s, âˆƒ b âˆˆ t, a \ b = c := by simp [(Â· \\ Â·)]

variable (s t)

@[simp, norm_cast] lemma coe_diffs : (â†‘(s \\ t) : Set Î±) = Set.image2 (Â· \ Â·) s t :=
  coe_imageâ‚‚ _ _ _

lemma card_diffs_le : (s \\ t).card â‰¤ s.card * t.card := card_imageâ‚‚_le _ _ _

lemma card_diffs_iff :
    (s \\ t).card = s.card * t.card â†” (s Ã—Ë¢ t : Set (Î± Ã— Î±)).InjOn fun x â†¦ x.1 \ x.2 :=
  card_imageâ‚‚_iff

variable {s sâ‚ sâ‚‚ t tâ‚ tâ‚‚ u}

lemma sdiff_mem_diffs : a âˆˆ s â†’ b âˆˆ t â†’ a \ b âˆˆ s \\ t := mem_imageâ‚‚_of_mem

lemma diffs_subset : sâ‚ âŠ† sâ‚‚ â†’ tâ‚ âŠ† tâ‚‚ â†’ sâ‚ \\ tâ‚ âŠ† sâ‚‚ \\ tâ‚‚ := imageâ‚‚_subset
lemma diffs_subset_left : tâ‚ âŠ† tâ‚‚ â†’ s \\ tâ‚ âŠ† s \\ tâ‚‚ := imageâ‚‚_subset_left
lemma diffs_subset_right : sâ‚ âŠ† sâ‚‚ â†’ sâ‚ \\ t âŠ† sâ‚‚ \\ t := imageâ‚‚_subset_right

lemma image_subset_diffs_left : b âˆˆ t â†’ (s.image fun a â†¦ a \ b) âŠ† s \\ t :=
  image_subset_imageâ‚‚_left

lemma image_subset_diffs_right : a âˆˆ s â†’ t.image (a \ Â·) âŠ† s \\ t :=
  image_subset_imageâ‚‚_right (f := (Â· \ Â·))

lemma forall_mem_diffs {p : Î± â†’ Prop} : (âˆ€ c âˆˆ s \\ t, p c) â†” âˆ€ a âˆˆ s, âˆ€ b âˆˆ t, p (a \ b) :=
  forall_imageâ‚‚_iff

@[simp] lemma diffs_subset_iff : s \\ t âŠ† u â†” âˆ€ a âˆˆ s, âˆ€ b âˆˆ t, a \ b âˆˆ u := imageâ‚‚_subset_iff

@[simp] lemma diffs_nonempty : (s \\ t).Nonempty â†” s.Nonempty âˆ§ t.Nonempty := imageâ‚‚_nonempty_iff

protected lemma Nonempty.diffs : s.Nonempty â†’ t.Nonempty â†’ (s \\ t).Nonempty := Nonempty.imageâ‚‚

lemma Nonempty.of_diffs_left : (s \\ t).Nonempty â†’ s.Nonempty := Nonempty.of_imageâ‚‚_left
lemma Nonempty.of_diffs_right : (s \\ t).Nonempty â†’ t.Nonempty := Nonempty.of_imageâ‚‚_right

@[simp] lemma empty_diffs : âˆ… \\ t = âˆ… := imageâ‚‚_empty_left
@[simp] lemma diffs_empty : s \\ âˆ… = âˆ… := imageâ‚‚_empty_right
@[simp] lemma diffs_eq_empty : s \\ t = âˆ… â†” s = âˆ… âˆ¨ t = âˆ… := imageâ‚‚_eq_empty_iff

@[simp] lemma singleton_diffs : {a} \\ t = t.image fun b â†¦ a \ b := imageâ‚‚_singleton_left
@[simp] lemma diffs_singleton : s \\ {b} = s.image fun a â†¦ a \ b := imageâ‚‚_singleton_right
lemma singleton_diffs_singleton : ({a} \\ {b} : Finset Î±) = {a \ b} := imageâ‚‚_singleton

lemma diffs_union_left : (sâ‚ âˆª sâ‚‚) \\ t = sâ‚ \\ t âˆª sâ‚‚ \\ t := imageâ‚‚_union_left
lemma diffs_union_right : s \\ (tâ‚ âˆª tâ‚‚) = s \\ tâ‚ âˆª s \\ tâ‚‚ := imageâ‚‚_union_right

lemma diffs_inter_subset_left : (sâ‚ âˆ© sâ‚‚) \\ t âŠ† sâ‚ \\ t âˆ© sâ‚‚ \\ t := imageâ‚‚_inter_subset_left
lemma diffs_inter_subset_right : s \\ (tâ‚ âˆ© tâ‚‚) âŠ† s \\ tâ‚ âˆ© s \\ tâ‚‚ := imageâ‚‚_inter_subset_right

lemma subset_diffs {s t : Set Î±} :
    â†‘u âŠ† Set.image2 (Â· \ Â·) s t â†’ âˆƒ s' t' : Finset Î±, â†‘s' âŠ† s âˆ§ â†‘t' âŠ† t âˆ§ u âŠ† s' \\ t' :=
  subset_imageâ‚‚

variable (s t u)

lemma biUnion_image_sdiff_left : s.biUnion (fun a â†¦ t.image $ (Â· \ Â·) a) = s \\ t :=
  biUnion_image_left

lemma biUnion_image_sdiff_right : t.biUnion (fun b â†¦ s.image fun a â†¦ a \ b) = s \\ t :=
  biUnion_image_right

lemma image_sdiff_product (s t : Finset Î±) : (s Ã—Ë¢ t).image (uncurry (Â· \ Â·)) = s \\ t :=
  image_uncurry_product _ _ _

lemma diffs_right_comm : s \\ t \\ u = s \\ u \\ t := imageâ‚‚_right_comm sdiff_right_comm

end Diffs

section Compls
variable [BooleanAlgebra Î±] (s sâ‚ sâ‚‚ t tâ‚ tâ‚‚ u v : Finset Î±)

/-- `s` is the finset of elements of the form `a âŠ“ b` where `a âˆˆ s`, `b âˆˆ t`. -/
def compls : Finset Î± â†’ Finset Î± := map âŸ¨compl, compl_injectiveâŸ©

@[inherit_doc]
scoped[FinsetFamily] postfix:max "á¶œË¢" => Finset.compls

open FinsetFamily

variable {s t} {a b c : Î±}

@[simp] lemma mem_compls : a âˆˆ sá¶œË¢ â†” aá¶œ âˆˆ s := by
  rw [Iff.comm, â†mem_map' âŸ¨compl, compl_injectiveâŸ©, Embedding.coeFn_mk, compl_compl, compls]

variable (s t)

@[simp] lemma image_compl : s.image compl = sá¶œË¢ := by simp [compls, map_eq_image]

@[simp, norm_cast] lemma coe_compls : (â†‘sá¶œË¢ : Set Î±) = compl '' â†‘s := coe_map _ _

@[simp] lemma card_compls : sá¶œË¢.card = s.card := card_map _

variable {s sâ‚ sâ‚‚ t tâ‚ tâ‚‚ u}

lemma compl_mem_compls : a âˆˆ s â†’ aá¶œ âˆˆ sá¶œË¢ := mem_map_of_mem _
@[simp] lemma compls_subset_compls : sâ‚á¶œË¢ âŠ† sâ‚‚á¶œË¢ â†” sâ‚ âŠ† sâ‚‚ := map_subset_map
lemma forall_mem_compls {p : Î± â†’ Prop} : (âˆ€ a âˆˆ sá¶œË¢, p a) â†” âˆ€ a âˆˆ s, p aá¶œ := forall_mem_map
lemma exists_compls_iff {p : Î± â†’ Prop} : (âˆƒ a âˆˆ sá¶œË¢, p a) â†” âˆƒ a âˆˆ s, p aá¶œ := by aesop

@[simp] lemma compls_compls (s : Finset Î±) : sá¶œË¢á¶œË¢ = s := by ext; simp

lemma compls_subset_iff : sá¶œË¢ âŠ† t â†” s âŠ† tá¶œË¢ := by rw [â†compls_subset_compls, compls_compls]

@[simp] lemma compls_nonempty : sá¶œË¢.Nonempty â†” s.Nonempty := map_nonempty

protected alias âŸ¨Nonempty.of_compls, Nonempty.complsâŸ© := compls_nonempty

@[simp] lemma compls_empty : (âˆ… : Finset Î±)á¶œË¢ = âˆ… := map_empty _
@[simp] lemma compls_eq_empty : sá¶œË¢ = âˆ… â†” s = âˆ… := map_eq_empty
@[simp] lemma compls_singleton (a : Î±) : {a}á¶œË¢ = {aá¶œ} := map_singleton _ _
@[simp] lemma compls_univ [Fintype Î±] : (univ : Finset Î±)á¶œË¢ = univ := by ext; simp
@[simp] lemma compls_union (s t : Finset Î±) : (s âˆª t)á¶œË¢ = sá¶œË¢ âˆª tá¶œË¢ := map_union _ _
@[simp] lemma compls_inter (s t : Finset Î±) : (s âˆ© t)á¶œË¢ = sá¶œË¢ âˆ© tá¶œË¢ := map_inter _ _

@[simp] lemma compls_infs (s t : Finset Î±) : (s âŠ¼ t)á¶œË¢ = sá¶œË¢ âŠ» tá¶œË¢ := by
  simp_rw [â†image_compl]; exact image_imageâ‚‚_distrib Î» _ _ â†¦ compl_inf

@[simp] lemma compls_sups (s t : Finset Î±) : (s âŠ» t)á¶œË¢ = sá¶œË¢ âŠ¼ tá¶œË¢ := by
  simp_rw [â†image_compl]; exact image_imageâ‚‚_distrib Î» _ _ â†¦ compl_sup

@[simp] lemma infs_compls_eq_diffs (s t : Finset Î±) : s âŠ¼ tá¶œË¢ = s \\ t := by
  ext; simp [sdiff_eq]; aesop

@[simp] lemma compls_infs_eq_diffs (s t : Finset Î±) : sá¶œË¢ âŠ¼ t = t \\ s := by
  rw [infs_comm, infs_compls_eq_diffs]

@[simp] lemma diffs_compls_eq_infs (s t : Finset Î±) : s \\ tá¶œË¢ = s âŠ¼ t := by
  rw [â†infs_compls_eq_diffs, compls_compls]

variable [Fintype Î±] {ğ’œ : Finset (Finset Î±)} {n : â„•}

protected lemma _root_.Set.Sized.compls (hğ’œ : (ğ’œ : Set (Finset Î±)).Sized n) :
    (ğ’œá¶œË¢ : Set (Finset Î±)).Sized (Fintype.card Î± - n) :=
  Finset.forall_mem_compls.2 $ Î» s hs â†¦ by rw [Finset.card_compl, hğ’œ hs]

lemma sized_compls (hn : n â‰¤ Fintype.card Î±) :
    (ğ’œá¶œË¢ : Set (Finset Î±)).Sized n â†” (ğ’œ : Set (Finset Î±)).Sized (Fintype.card Î± - n) where
  mp hğ’œ := by simpa using hğ’œ.compls
  mpr hğ’œ := by simpa only [tsub_tsub_cancel_of_le hn] using hğ’œ.compls

end Compls
end Finset
