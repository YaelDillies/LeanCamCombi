import Mathlib.Data.Finset.Lattice

-- attribute [protected] Finset.sup_le
-- attribute [protected] Finset.inf_le
-- attribute [protected] Finset.inf_eq_top_iff

namespace Finset
variable {Î¹ Îº Î± Î² : Type*}

section SemilatticeSup
variable [SemilatticeSup Î±] [SemilatticeSup Î²] {s : Finset Î¹} {t : Finset Îº}

/-- See also `Finset.sup'_prod_map`. -/
lemma prod_mk_sup'_sup' (hs : s.Nonempty) (ht : t.Nonempty) (f : Î¹ â†’ Î±) (g : Îº â†’ Î²) :
    (sup' s hs f, sup' t ht g) = sup' (s Ã—Ë¢ t) (hs.product ht) (Prod.map f g) :=
  eq_of_forall_ge_iff Î» a â†¦ by
    simp only [Prod_map, sup'_le_iff, mem_product, and_imp, Prod.forall, Prod.le_def]
    obtain âŸ¨a, haâŸ© := hs
    obtain âŸ¨b, hbâŸ© := ht
    exact âŸ¨by aesop, Î» h â†¦ âŸ¨Î» i hi â†¦ (h _ _ hi hb).1, Î» j hj â†¦ (h _ _ ha hj).2âŸ©âŸ©

/-- See also `Finset.prod_mk_sup'_sup'`. -/
@[simp] lemma sup'_prod_map (hst : (s Ã—Ë¢ t).Nonempty) (f : Î¹ â†’ Î±) (g : Îº â†’ Î²) :
    sup' (s Ã—Ë¢ t) hst (Prod.map f g) = (sup' s hst.fst f, sup' t hst.snd g) :=
  (prod_mk_sup'_sup' _ _ _ _).symm

end SemilatticeSup

section SemilatticeInf
variable [SemilatticeInf Î±] [SemilatticeInf Î²] {s : Finset Î¹} {t : Finset Îº}

/-- See also `Finset.inf'_prod_map`. -/
lemma prod_mk_inf'_inf' (hs : s.Nonempty) (ht : t.Nonempty) (f : Î¹ â†’ Î±) (g : Îº â†’ Î²) :
    (inf' s hs f, inf' t ht g) = inf' (s Ã—Ë¢ t) (hs.product ht) (Prod.map f g) :=
  eq_of_forall_le_iff Î» a â†¦ by
    simp only [Prod_map, le_inf'_iff, mem_product, and_imp, Prod.forall, Prod.le_def]
    obtain âŸ¨a, haâŸ© := hs
    obtain âŸ¨b, hbâŸ© := ht
    exact âŸ¨by aesop, Î» h â†¦ âŸ¨Î» i hi â†¦ (h _ _ hi hb).1, Î» j hj â†¦ (h _ _ ha hj).2âŸ©âŸ©

/-- See also `Finset.prod_mk_inf'_inf'`. -/
@[simp] lemma inf'_prod_map (hst : (s Ã—Ë¢ t).Nonempty) (f : Î¹ â†’ Î±) (g : Îº â†’ Î²) :
    inf' (s Ã—Ë¢ t) hst (Prod.map f g) = (inf' s hst.fst f, inf' t hst.snd g) :=
  (prod_mk_inf'_inf' _ _ _ _).symm

end SemilatticeInf

@[elab_as_elim]
protected lemma family_induction_on {p : Finset (Finset Î±) â†’ Prop} [DecidableEq Î±]
    (ğ’œ : Finset (Finset Î±)) (empty : p âˆ…) (singleton_empty : p {âˆ…})
    (image_insert : âˆ€ (a : Î±) (ğ’œ : Finset (Finset Î±)),
      (âˆ€ s âˆˆ ğ’œ, a âˆ‰ s) â†’ p ğ’œ â†’ p (ğ’œ.image $ insert a))
    (subfamily : âˆ€ (a : Î±) â¦ƒğ’œ : Finset (Finset Î±)â¦„,
      p (ğ’œ.filter (a âˆ‰ Â·)) â†’ p (ğ’œ.filter (a âˆˆ Â·)) â†’ p ğ’œ) :
    p ğ’œ := by
  set u := ğ’œ.sup id
  have hu : âˆ€ s âˆˆ ğ’œ, s âŠ† u := fun s â†¦ le_sup (f := id)
  clear_value u
  induction' u using Finset.induction with a u _ ih generalizing ğ’œ
  Â· simp_rw [subset_empty] at hu
    rw [â†subset_singleton_iff', subset_singleton_iff] at hu
    obtain rfl | rfl := hu <;> assumption
  refine subfamily a (ih _ ?_) ?_
  Â· simp only [not_not, mem_filter, and_imp]
    exact fun s hs has â†¦ (subset_insert_iff_of_not_mem has).1 $ hu _ hs
  Â· have := image_insert a ((ğ’œ.filter (a âˆˆ Â·)).image (erase Â· a))
      (forall_image.2 fun s _ â†¦ not_mem_erase _ _)
      (ih _ $ forall_image.2 fun s hs â†¦ subset_insert_iff.1 $ hu _ $ filter_subset _ _ hs)
    rwa [image_image, Finset.image_congr, image_id] at this
    rintro s hs
    exact insert_erase $ (mem_filter.1 hs).2

end Finset
