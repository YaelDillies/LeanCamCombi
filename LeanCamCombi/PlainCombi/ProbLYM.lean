/-
Copyright (c) 2024 Ching-Tsun Chou, Chris Wong. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ching-Tsun Chou, Chris Wong
-/
import LeanCamCombi.Mathlib.Data.Fintype.Card
import Mathlib.Data.Fintype.Perm
import Mathlib.Probability.UniformOn

/-!
# The LYM inequality using probability theory

This file proves the LYM inequality using (very elementary) probability theory.

## References

This proof formalizes Section 1.2 of Prof. Yufei Zhao's lecture notes for MIT 18.226:

<https://yufeizhao.com/pm/probmethod_notes.pdf>

A video of Prof. Zhao's lecture is available on YouTube:

<https://youtu.be/exBXHYl4po8>

The proof of Theorem 1.10, Lecture 3 in the Cambridge lecture notes on combinatorics:

<https://github.com/YaelDillies/maths-notes/blob/master/combinatorics.pdf>

is basically the same proof, except without using probability theory.
-/

open BigOperators Fintype Finset Set MeasureTheory ProbabilityTheory
open MeasureTheory.Measure
open scoped ENNReal

noncomputable section

/-- A numbering is a bijective map from a finite type or set to a Fin type
of the same cardinality.  We cannot use the existing notion of permutations
in mathlib because we need the special property `set_prefix_subset` below. -/

@[reducible]
def Numbering (Î± : Type*) [Fintype Î±] := Î± â‰ƒ Fin (card Î±)

variable {Î± : Type*} [Fintype Î±] [DecidableEq Î±]

theorem card_Numbering : card (Numbering Î±) = (card Î±).factorial := by
  exact Fintype.card_equiv (Fintype.equivFinOfCardEq rfl)

/-- `IsPrefix s f` means that the elements of `s` precede the elements of `sá¶œ`
in the numbering `f`. -/
def IsPrefix (s : Finset Î±) (f : Numbering Î±) :=
  âˆ€ x, x âˆˆ s â†” f x < #s

omit [DecidableEq Î±] in
theorem subset_IsPrefix_IsPrefix {s1 s2 : Finset Î±} {f : Numbering Î±}
  (h_s1 : IsPrefix s1 f) (h_s2 : IsPrefix s2 f) (h_card : s1.card â‰¤ s2.card) : s1 âŠ† s2 := by
  intro a h_as1
  exact (h_s2 a).mpr (lt_of_lt_of_le ((h_s1 a).mp h_as1) h_card)

def equiv_IsPrefix_Numbering2 (s : Finset Î±) :
    {f // IsPrefix s f} â‰ƒ Numbering s Ã— Numbering â†‘(sá¶œ) where
  toFun := fun âŸ¨f, hfâŸ© â†¦
    { fst.toFun x := âŸ¨f x, by simp [â† hf x]âŸ©
      fst.invFun n :=
        âŸ¨f.symm âŸ¨n, n.2.trans_le <| by simpa using s.card_le_univâŸ©, by rw [hf]; simpa using n.2âŸ©
      fst.left_inv x := by simp
      fst.right_inv n := by simp
      snd.toFun x := âŸ¨f x - #s, by
        have := (hf x).not.1 (Finset.mem_compl.1 x.2)
        simp at this âŠ¢
        omegaâŸ©
      snd.invFun n :=
        âŸ¨f.symm âŸ¨n + #s, Nat.add_lt_of_lt_sub <| by simpa using n.2âŸ©, by rw [s.mem_compl, hf]; simpâŸ©
      snd.left_inv := by
        rintro âŸ¨x, hxâŸ©
        rw [s.mem_compl, hf, not_lt] at hx
        simp [Nat.sub_add_cancel hx]
      snd.right_inv := by rintro âŸ¨n, hnâŸ©; simp }
  invFun := fun (g, g') â†¦
    { val.toFun x :=
        if hx : x âˆˆ s then
          g âŸ¨x, hxâŸ© |>.castLE Fintype.card_subtype_le'
        else
          g' âŸ¨x, by simpaâŸ© |>.addNat #s |>.cast (by simp [card_le_univ])
      val.invFun n :=
        if hn : n < #s then
          g.symm âŸ¨n, by simpa using hnâŸ©
        else
          g'.symm âŸ¨n - #s, by simp; omegaâŸ©
      val.left_inv x := by
        by_cases hx : x âˆˆ s
        Â· have : g âŸ¨x, hxâŸ© < #s := by simpa using (g âŸ¨x, hxâŸ©).2
          simp [hx, this]
        Â· simp [hx, Equiv.symm_apply_eq]
      val.right_inv n := by
        obtain hns | hsn := lt_or_le n.1 #s
        Â· simp [hns]
        Â· simp [hsn.not_lt, hsn, dif_neg (mem_compl.1 <| Subtype.prop _), Fin.ext_iff,
            Fintype.card_subtype_le']
      property x := by
        constructor
        Â· intro hx
          simpa [hx, -Fin.is_lt] using (g _).is_lt
        Â· by_cases hx : x âˆˆ s <;> simp [hx] }
  left_inv := by
    rintro âŸ¨f, hfâŸ©
    ext x
    by_cases hx : x âˆˆ s
    Â· simp [hx]
    Â· rw [hf, not_lt] at hx
      simp [hx]
  right_inv := by
    rintro âŸ¨g, g'âŸ©
    simp
    constructor
    Â· ext x
      simp
    Â· ext x
      rcases x with âŸ¨x, hxâŸ©
      rw [Finset.mem_compl] at hx
      simp [hx]

instance (s : Finset Î±) :
    DecidablePred fun f â†¦ (IsPrefix s f) := by
  intro f ; exact Classical.propDecidable ((fun f â†¦ IsPrefix s f) f)

def PrefixedNumbering (s : Finset Î±) : Finset (Numbering Î±) :=
  {f | IsPrefix s f}

theorem card_PrefixedNumbering (s : Finset Î±) :
    (PrefixedNumbering s).card = (#s).factorial * (card Î± - #s).factorial := by
  have h_eq:= Fintype.card_congr (equiv_IsPrefix_Numbering2 s)
  rw [Fintype.card_subtype] at h_eq
  simp [PrefixedNumbering, h_eq, card_Numbering]

private lemma auxLemma {k m n : â„•} (hn : 0 < n) (heq : k * m = n) :
    (â†‘ m : ENNReal) / (â†‘ n : ENNReal) = 1 / (â†‘ k : ENNReal) := by
  -- The following proof is due to Aaron Liu.
  subst heq
  have hm : m â‰  0 := by rintro rfl ; simp at hn
  have hk : k â‰  0 := by rintro rfl ; simp at hn
  refine (ENNReal.toReal_eq_toReal ?_ ?_).mp ?_
  Â· intro h
    apply_fun ENNReal.toReal at h
    simp [hm, hk] at h
  Â· intro h
    apply_fun ENNReal.toReal at h
    simp [hk] at h
  Â· field_simp
    ring

instance : MeasurableSpace (Numbering Î±) := âŠ¤

theorem count_PrefixedNumbering (s : Finset Î±) :
    count (PrefixedNumbering s).toSet = â†‘((#s).factorial * (card Î± - #s).factorial) := by
  rw [â† card_PrefixedNumbering s, count_apply_finset]

theorem prob_PrefixedNumbering (s : Finset Î±) :
    uniformOn Set.univ (PrefixedNumbering s).toSet = 1 / (card Î±).choose #s := by
  rw [uniformOn_univ, count_PrefixedNumbering s, card_Numbering]
  apply auxLemma (Nat.factorial_pos (card Î±))
  rw [â† mul_assoc]
  exact Nat.choose_mul_factorial_mul_factorial (Finset.card_le_univ s)

theorem disj_PrefixedNumbering {s t : Finset Î±} (h_st : Â¬ s âŠ† t) (h_ts : Â¬ t âŠ† s) :
    Disjoint (PrefixedNumbering s).toSet (PrefixedNumbering t).toSet := by
  refine Set.disjoint_iff.mpr ?_
  intro p
  simp only [mem_inter_iff, Finset.mem_coe, mem_empty_iff_false, imp_false, not_and]
  simp [PrefixedNumbering]
  intro h_s h_t
  rcases Nat.le_total #s t.card with h_st' | h_ts'
  Â· exact h_st (subset_IsPrefix_IsPrefix h_s h_t h_st')
  Â· exact h_ts (subset_IsPrefix_IsPrefix h_t h_s h_ts')

variable {ð“ : Finset (Finset Î±)}

theorem prob_biUnion_antichain (hð“ : IsAntichain (Â· âŠ† Â·) ð“.toSet) :
    uniformOn Set.univ (â‹ƒ s âˆˆ ð“, (PrefixedNumbering s).toSet) =
    âˆ‘ s âˆˆ ð“, uniformOn Set.univ (PrefixedNumbering s).toSet := by
  have hd : ð“.toSet.PairwiseDisjoint (fun s â†¦ (PrefixedNumbering s).toSet) := by
    intro s h_s t h_t h_ne
    simp only [Function.onFun]
    have h_st := hð“ h_s h_t h_ne
    have h_ts := hð“ h_t h_s h_ne.symm
    exact disj_PrefixedNumbering h_st h_ts
  have hm : âˆ€ s âˆˆ ð“, MeasurableSet (PrefixedNumbering s).toSet := by
    intro s h_s ; exact trivial
  rw [measure_biUnion_finset hd hm (Î¼ := uniformOn Set.univ)]

theorem LYM_inequality (hð“ : IsAntichain (Â· âŠ† Â·) ð“.toSet) :
    âˆ‘ s âˆˆ ð“, ((1 : ENNReal) / (card Î±).choose #s) â‰¤ 1 := by
  have h1 s (hs : s âˆˆ ð“) :
      (1 : ENNReal) / (card Î±).choose #s = uniformOn Set.univ (PrefixedNumbering s).toSet := by
    rw [prob_PrefixedNumbering]
  rw [Finset.sum_congr (rfl : ð“ = ð“) h1, â† prob_biUnion_antichain hð“]
  exact prob_le_one

end
